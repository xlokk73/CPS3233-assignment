GLOBAL {
	VARIABLES {
		Clock x = new Clock();	%% Checks for Bad State: BadMoving
		Clock y = new Clock();  %% Checks for Bad State: BadLoading
	}
	
	EVENTS {				
		setMoving(boolean moving) = {*.setMoving(moving)}	%% goUp/goDown 
		closeDoors() = {*.closeDoors()}						%% closeDoor
		openDoors() = {*.openDoors()}						%% openDoor
		setFloor() = {*.setFloor(*)} 						%% stop
		callLiftToFloor() = {*.callLiftToFloor(*)}			%% requested
	}
	
	PROPERTY TimeProperties{
		STATES {
			BAD {
				BadLoading {System.out.println("Oqqow!! lift open too long");}
				BadMoving {System.out.println("Oqqow!! lift took too long to start moving");}
			}
			
			NORMAL {
				Loading
				Moving 
				Requested 
				LoadingRequested
			}
			
			STARTING {
				Idle
			}
		}
		
		TRANSITIONS {			
			
			Idle -> Loading [openDoors \ \ y.reset(); ]
			Idle -> Requested [callLiftToFloor \ \ x.reset(); ]
			
			Loading -> Idle [closeDoors \ y.compareTo(3) < 0 \ x.reset();]
			Loading -> BadLoading [closeDoors \ y.compareTo(3) >= 0 \ ]
			Loading -> LoadingRequested [callLiftToFloor \ \ ]
			
			Moving -> Idle [setFloor \ \ x.reset();]
			Moving -> Requested [callLiftToFloor \ \ x.reset();]
			
			Requested -> Moving [setMoving \ moving==true && x.compareTo(3)<0 \ ]
			Requested -> BadMoving [setMoving \ moving==true && x.compareTo(3)>=0 \ ]
			
			LoadingRequested -> Requested [closeDoors \ y.compareTo(3)<0 \ x.reset(); ]
			LoadingRequested -> BadLoading [closeDoors \ y.compareTo(3)>=0 \]
			
		}
	}

	
}