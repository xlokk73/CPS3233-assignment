
IMPORTS {
	import com.liftmania.Lift;
}

GLOBAL {
	
	FOREACH (Lift lift) {
		VARIABLES {
			Clock x = new Clock();	%% Checks for Bad State: BadMoving
			Clock y = new Clock();  %% Checks for Bad State: BadLoading
		}
		
		EVENTS {				
			
			setMoving(isMoving) = {Lift l.setMoving(boolean isMoving)} where {lift = l;} %%goUp/goDown
			closeDoors() = {Lift l.closeDoors()} where {lift = l;}			%% closeDoor
			openDoors() = {Lift l.openDoors()} where {lift = l;}	%% openDoor
			setFloor() = {Lift l.setFloor(*)} where {lift = l;}		%% stop
			callLiftToFloor(Lift l) = {*.callLiftToFloor(*)uponReturning(l)} where {lift = l;} %% summonRequest
		}
		
		PROPERTY LiftOpenTimeProperty {
			STATES {
				BAD {
					OpenTooLong
					MovingWhileLoading
					ConsecutiveDoorSignals
				}
				
				NORMAL {
					Loading
					Moving
				}
				
				STARTING {
					Idle
				}
			}
			
			TRANSITIONS {
				Idle -> Loading [openDoors \ \ y.reset();]
				Idle -> Moving [setMoving \ isMoving \ ]
				Idle -> ConsecutiveDoorSignals [closeDoors \ \ ]
				
				Loading -> Idle [closeDoors \ y.compareTo(3)<0 \ ]
				Loading -> OpenTooLong [closeDoors \ y.compareTo(3)>=0 \]
				Loading -> MovingWhileLoading [setMoving \ \ ]
				Loading -> ConsecutiveDoorSignals [openDoors \ \ ]
				
				Moving -> Idle [setFloor \ \]
				Moving -> MovingWhileLoading [openDoors \ \ ]
				Moving -> ConsecutiveDoorSignals [closeDoors \ \ ]
			}
		}
		
		PROPERTY StartMovingTimeProperty {
		
			STATES {
				BAD {
					BadMoving {System.out.println("Oqqow!! lift took too long to start moving");}
					
				}
				
				NORMAL {
					Loading
					Moving 
					Requested 
					LoadingRequested
				}
				
				STARTING {
					Idle
				}
			}
			
			TRANSITIONS {			
				
				
				Idle -> Loading [openDoors \ \ ]
				Idle -> Requested [callLiftToFloor \ \ x.reset(); ]
				Idle -> Moving [setMoving \ isMoving \ ]
				
				Loading -> Idle [closeDoors \ \ x.reset();]
				Loading -> LoadingRequested [callLiftToFloor \ \ ]
				
				Moving -> Idle [setFloor \ \ x.reset();]
				Moving -> LoadingRequested [callLiftToFloor \ \ ]
				
				Requested -> Moving [setMoving \ isMoving && x.compareTo(3)<0 \ ]
				Requested -> BadMoving [setMoving \ isMoving && x.compareTo(3)>=0 \ ]
				Requested -> Requested [callLiftToFloor \ \ ]
				
				LoadingRequested -> Requested [closeDoors \ \ x.reset(); ]
				
				Idle -> Loading [openDoors \ \ y.reset(); ]
				Idle -> Requested [callLiftToFloor \ \ x.reset(); ]
				
				
			}
		}

		

	}
}
